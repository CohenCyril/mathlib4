/-
Copyright (c) 2024 Yuma Mizuno. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yuma Mizuno
-/
import Mathlib.CategoryTheory.Bicategory.Kan.IsKan

/-!
# Existence of Kan extensions and Kan lifts in bicategories

We provide the propositional typeclass `HasLeftKanExtension f g`, which asserts that there
exists a left Kan extension of `g` along `f`. Under the assumption that `HasLeftKanExtension f g`,
we define the left Kan extension `lan f g` by using the axiom of choice.

## Main definitions

* `lan f g` is the left Kan extension of `g` along `f`, and is denoted by `f ‚Å∫ g`.
* `lanLift f g` is the left Kan lift of `g` along `f`, and is denoted by `f ‚Çä g`.

-/

noncomputable section

namespace CategoryTheory

namespace Bicategory

universe w v u

variable {B : Type u} [Bicategory.{w, v} B] {a b c : B}

section LeftKan

open LeftExtension

variable {f : a ‚ü∂ b} {g : a ‚ü∂ c}

/-- Left Kan extensions of `g` along `f`. -/
structure LeftKanExtension (f : a ‚ü∂ b) (g : a ‚ü∂ c) where
  /-- The underlying left extension. -/
  extension : LeftExtension f g
  /-- The proof that the extension is a Kan extension. -/
  isKan : extension.IsKan

/-- The existence of a left Kan extension of `g` along `f`. -/
class HasLeftKanExtension (f : a ‚ü∂ b) (g : a ‚ü∂ c) : Prop where mk' ::
  exists_leftKan : Nonempty (LeftKanExtension f g)

theorem HasLeftKanExtension.mk {t : LeftExtension f g} (H : IsKan t) : HasLeftKanExtension f g :=
  ‚ü®‚ü®t, H‚ü©‚ü©

/-- Use the axiom of choice to extract the explicit left kan extension. -/
def getLeftKanExtension (f : a ‚ü∂ b) (g : a ‚ü∂ c) [HasLeftKanExtension f g] : LeftKanExtension f g :=
  Classical.choice HasLeftKanExtension.exists_leftKan

/-- The left Kan extension of `g` along `f`. -/
def lan.extension (f : a ‚ü∂ b) (g : a ‚ü∂ c) [HasLeftKanExtension f g] : LeftExtension f g :=
  (getLeftKanExtension f g).extension

/-- The left Kan extension of `g` along `f`, denoted by `f ‚Å∫ g`.
```
  b
  ‚ñ≥ \
  |   \ f ‚Å∫ g
f |     \
  |       ‚óø
  a - - - ‚ñ∑ c
      g
```
-/
def lan (f : a ‚ü∂ b) (g : a ‚ü∂ c) [HasLeftKanExtension f g] : b ‚ü∂ c :=
  (lan.extension f g).extension

@[inherit_doc] scoped infixr:90 " ‚Å∫ " => lan

/-- The unit for the left kan extension `f ‚Å∫ g`. -/
def lan.unit (f : a ‚ü∂ b) (g : a ‚ü∂ c) [HasLeftKanExtension f g] : g ‚ü∂ f ‚â´ f ‚Å∫ g :=
  (lan.extension f g).unit

/-- Evidence that the `lan.extension f g` is a kan extension. -/
def lan.isLeftKan (f : a ‚ü∂ b) (g : a ‚ü∂ c) [HasLeftKanExtension f g] : (lan.extension f g).IsKan :=
  (getLeftKanExtension f g).isKan

variable {f : a ‚ü∂ b} {g : a ‚ü∂ c}

/-- The family of 2-morphisms out of the left Kan extension `f ‚Å∫ g`. -/
def lan.desc [HasLeftKanExtension f g] (s : LeftExtension f g) :
    f ‚Å∫ g ‚ü∂ s.extension :=
  (lan.isLeftKan f g).desc s

@[reassoc (attr := simp)]
theorem lan.lan‚ÇÇ_desc [HasLeftKanExtension f g] (s : LeftExtension f g) :
    lan.unit f g ‚â´ f ‚óÅ lan.desc s = s.unit :=
  (lan.isLeftKan f g).fac s

@[simp]
theorem lan.isLeftKan_desc [HasLeftKanExtension f g] (s : LeftExtension f g) :
    (lan.isLeftKan f g).desc s = lan.desc s :=
  rfl

theorem lan.existsUnique [HasLeftKanExtension f g] (s : LeftExtension f g) :
    ‚àÉ! œÑ, lan.unit f g ‚â´ f ‚óÅ œÑ = s.unit :=
  (lan.isLeftKan f g).existsUnique _

/-- Absolute left Kan extensions of `g` along `f`. -/
structure AbsLeftKan (f : a ‚ü∂ b) (g : a ‚ü∂ c) where
  /-- The underlying left extension. -/
  extension : LeftExtension f g
  /-- The proof that the extension is an absolute Kan extension. -/
  isAbsKan : extension.IsAbsKan

/-- The existence of an absolute left Kan extension of `g` along `f`. -/
class HasAbsLeftKan (f : a ‚ü∂ b) (g : a ‚ü∂ c) : Prop where mk' ::
  exists_absLeftKan : Nonempty (AbsLeftKan f g)

theorem HasAbsLeftKan.mk {t : LeftExtension f g} (H : IsAbsKan t) : HasAbsLeftKan f g := ‚ü®‚ü®t, H‚ü©‚ü©

/-- Use the axiom of choice to extract the explicit absolute left kan extension. -/
def absLan.getAbsLeftKanExtension (f : a ‚ü∂ b) (g : a ‚ü∂ c) [HasAbsLeftKan f g] : AbsLeftKan f g :=
  Classical.choice HasAbsLeftKan.exists_absLeftKan

instance [HasAbsLeftKan f g] {x : B} (h : c ‚ü∂ x) : HasLeftKanExtension f (g ‚â´ h) :=
  .mk <| (absLan.getAbsLeftKanExtension f g).isAbsKan h

instance [HasAbsLeftKan f g] : HasLeftKanExtension f g :=
  .mk (lan.isLeftKan f (g ‚â´ ùüô c)).ofAlongCompId

/-- If there exists an absolute Kan extension of `g` along `f`, we have the evidence that the
distinguished kan extension `lan.extension f g` is an absolute Kan extension. -/
def lan.isAbsLeftKan (f : a ‚ü∂ b) (g : a ‚ü∂ c) [HasAbsLeftKan f g] : (lan.extension f g).IsAbsKan :=
  let ‚ü®_, H‚ü© := absLan.getAbsLeftKanExtension f g
  H.ofIsoAbsKan <| IsKan.uniqueUpToIso H.isKan (lan.isLeftKan f g)

/-- `h : c ‚ü∂ x` commutes with the left Kan extension of `g : a ‚ü∂ c` along `f : a ‚ü∂ b`. -/
class CommuteWithLeftKan
    (f : a ‚ü∂ b) (g : a ‚ü∂ c) [HasLeftKanExtension f g] {x : B} (h : c ‚ü∂ x) : Prop where
  commute : Nonempty <| IsKan <| (lan.extension f g).whisker h

instance {x : B} {h : c ‚ü∂ x} [HasLeftKanExtension f g] [CommuteWithLeftKan f g h] :
    HasLeftKanExtension f (g ‚â´ h) :=
  .mk (Classical.choice <| CommuteWithLeftKan.commute)

end LeftKan

section LeftLift

open LeftLift

variable {f : b ‚ü∂ a} {g : c ‚ü∂ a}

/-- Left lifts of `g` along `f`. -/
structure LeftKanLift (f : b ‚ü∂ a) (g : c ‚ü∂ a) where
  /-- The underlying left lift. -/
  lift : LeftLift f g
  /-- The proof that the left lift is a Kan lift. -/
  isKan : lift.IsKan

/-- The existence of a left kan lift of `g` along `f`. -/
class HasLeftKanLift (f : b ‚ü∂ a) (g : c ‚ü∂ a) : Prop where mk' ::
  exists_leftKanLift : Nonempty (LeftKanLift f g)

theorem HasLeftKanLift.mk {t : LeftLift f g} (H : IsKan t) : HasLeftKanLift f g := ‚ü®‚ü®t, H‚ü©‚ü©

/-- Use the axiom of choice to extract the explicit left kan lift. -/
def getLeftKanLift (f : b ‚ü∂ a) (g : c ‚ü∂ a) [HasLeftKanLift f g] : LeftKanLift f g :=
  Classical.choice HasLeftKanLift.exists_leftKanLift

/-- The left kan lift of `g` along `f`. -/
def lanLift.lift (f : b ‚ü∂ a) (g : c ‚ü∂ a) [HasLeftKanLift f g] : LeftLift f g :=
  (getLeftKanLift f g).lift

/-- The left kan lift of `g` along `f`, denoted by `f ‚Çä g`.
```
            b
          ‚óπ |
  f ‚Çä g /   |
      /     | f
    /       ‚ñΩ
  c - - - ‚ñ∑ a
       g
```
-/
def lanLift (f : b ‚ü∂ a) (g : c ‚ü∂ a) [HasLeftKanLift f g] : c ‚ü∂ b :=
  (lanLift.lift f g).lift

@[inherit_doc] scoped infixr:90 " ‚Çä " => lanLift

/-- The unit for `lanLift f g`. -/
def lanLift.unit (f : b ‚ü∂ a) (g : c ‚ü∂ a) [HasLeftKanLift f g] : g ‚ü∂ f ‚Çä g ‚â´ f :=
  (lanLift.lift f g).unit

/-- Evidence that the `lanLift.lift f g` is a kan lift. -/
def lanLift.isLeftKan (f : b ‚ü∂ a) (g : c ‚ü∂ a) [HasLeftKanLift f g] : (lanLift.lift f g).IsKan :=
  (getLeftKanLift f g).isKan

variable {f : b ‚ü∂ a} {g : c ‚ü∂ a}

/-- The family of 2-morphisms out of the left kan lift `f ‚Çä g`. -/
def lanLift.desc [HasLeftKanLift f g] (s : LeftLift f g) :
    f ‚Çä g ‚ü∂ s.lift :=
  (lanLift.isLeftKan f g).desc s

@[reassoc (attr := simp)]
theorem lanLift.unit_desc [HasLeftKanLift f g] (s : LeftLift f g) :
    lanLift.unit f g ‚â´ lanLift.desc s ‚ñ∑ f = s.unit :=
  (lanLift.isLeftKan f g).fac s

@[simp]
theorem lanLift.isLeftKan_desc [HasLeftKanLift f g] (s : LeftLift f g) :
    (lanLift.isLeftKan f g).desc s = lanLift.desc s :=
  rfl

theorem lanLift.existsUnique [HasLeftKanLift f g] (s : LeftLift f g) :
    ‚àÉ! œÑ, lanLift.unit f g ‚â´ œÑ ‚ñ∑ f = s.unit :=
  (lanLift.isLeftKan f g).existsUnique _

/-- Absolute left lifts of `g` along `f`. -/
structure AbsLeftKanLift (f : b ‚ü∂ a) (g : c ‚ü∂ a) where
  /-- The underlying left lift. -/
  lift : LeftLift f g
  /-- The proof that the left lift is an absolute Kan lift. -/
  isAbsKan : lift.IsAbsKan

/-- The existence of an absolute left Kan lift of `g` along `f`. -/
class HasAbsLeftKanLift (f : b ‚ü∂ a) (g : c ‚ü∂ a) : Prop where mk' ::
  exists_absLeftKanLift : Nonempty (AbsLeftKanLift f g)

theorem HasAbsLeftKanLift.mk {t : LeftLift f g} (H : IsAbsKan t) : HasAbsLeftKanLift f g :=
  ‚ü®‚ü®t, H‚ü©‚ü©

/-- Use the axiom of choice to extract the explicit absolute left kan lift. -/
def absLanLift.getAbsLeftKanLift (f : b ‚ü∂ a) (g : c ‚ü∂ a) [HasAbsLeftKanLift f g] :
    AbsLeftKanLift f g :=
  Classical.choice HasAbsLeftKanLift.exists_absLeftKanLift

instance [HasAbsLeftKanLift f g] {x : B} (h : x ‚ü∂ c) : HasLeftKanLift f (h ‚â´ g) :=
  .mk <| (absLanLift.getAbsLeftKanLift f g).isAbsKan h

instance [HasAbsLeftKanLift f g] : HasLeftKanLift f g :=
  .mk (lanLift.isLeftKan f (ùüô c ‚â´ g)).ofAlongIdComp

/-- If there exists an absolute Kan lift of `g` along `f`, we have the evidence that the
distinguished kan lift `lanLift.lift f g` is an absolute Kan lift. -/
def lanLift.isAbsLeftKan (f : b ‚ü∂ a) (g : c ‚ü∂ a) [HasAbsLeftKanLift f g] :
    (lanLift.lift f g).IsAbsKan :=
  let ‚ü®_, H‚ü© := absLanLift.getAbsLeftKanLift f g
  fun h ‚Ü¶ (H h).ofIso (whiskerIso (IsKan.uniqueUpToIso (H.isKan) (lanLift.isLeftKan f g)) h)

/-- `h : x ‚ü∂ b` commutes with the left kan lift of `g : c ‚ü∂ a` along `f : b ‚ü∂ a`. -/
class CommuteWithLeftKanLift
    (f : b ‚ü∂ a) (g : c ‚ü∂ a) [HasLeftKanLift f g] {x : B} (h : x ‚ü∂ c) : Prop where
  commute : Nonempty <| IsKan <| (lanLift.lift f g).whisker h

instance {x : B} {h : x ‚ü∂ c} [HasLeftKanLift f g] [CommuteWithLeftKanLift f g h] :
    HasLeftKanLift f (h ‚â´ g) :=
  .mk (Classical.choice <| CommuteWithLeftKanLift.commute)

end LeftLift

end Bicategory

end CategoryTheory
